plugins {
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.0.119'
    id 'java-library'
}

tasks.named('wrapper', Wrapper).configure {
    //Define wrapper values here so as to not have to always do so when updating gradlew.properties
    gradleVersion = '8.7'
    distributionType = Wrapper.DistributionType.ALL
}

idea {
    module {
        //Exclude directories from being managed
        for (String excludeDirName in ["runs", "out", "logs", "gradle"]) {
            excludeDirs.add(new File(projectDir, excludeDirName))
        }
        //Tell IDEA to always download sources/javadoc artifacts from maven.
        downloadJavadoc = true
        downloadSources = true
    }
}

version = minecraft_version + '-' + mod_version
group = mod_author + '.' + mod_id // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = mod_id + "-neoforge"

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)

minecraft.accessTransformers.file file('src/main/resources/META-INF/accesstransformer.cfg')

sourceSets.main.resources {
    srcDirs += 'src/generated/resources'
    exclude '.cache'
}

def version_replaces = [
        "version"    : mod_version,
        "mc_version": minecraft_version_range,
        "neoforge_version": neoforge_version_range,
        "loader_version": loader_version_range
]
processResources {
    duplicatesStrategy = DuplicatesStrategy.FAIL
    inputs.properties(version_replaces)
    filesMatching("META-INF/neoforge.mods.toml") {
        expand version_replaces
    }
    exclude ".cache"
}

runs {
    configureEach {
        workingDirectory project.file("run/${it.name}")
        //systemProperty 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
        //systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client

    server {
        programArgument '--nogui'
    }

    data {
        programArguments.addAll '--mod', "peaks", '--all', '--output', file('src/generated/resources/').getAbsolutePath(),
                '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

repositories {

}


dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"
}

//Minimize/optimize all png files, requires optipng on the PATH
// Credits: BrainStone
void minimizePNGFile(File file) {
    long size = file.length()
    exec {
        executable "optipng"
        args "-q", "-o7", "-zm1-9", "-strip", "all", file
    }
    long newSize = file.length()
    if (newSize < size) {
        System.out.format("Reduced File size of %s from %d bytes to %d bytes (reduced by %.2f%%)\n",
                file, size, newSize, ((double) (size - newSize)) / ((double) size) * 100.0)
    }
}

task optimizePng {
    def pngPatterns = ["**/*.png"]
    doLast {
        //Ensure the logo is minimized (we add this file to each jar)
        minimizePNGFile(file("${projectDir}/logo.png"))
        //Minimize any PNGs in the source sets
        for (dir in sourceSets.main.resources.srcDirs) {
            fileTree(dir: dir, includes: pngPatterns).each { minimizePNGFile(it) }
        }
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_author,
                "Specification-Version"   : mod_version,
                "Implementation-Title"    : project.jar.archiveBaseName,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

compileJava {
    options.compilerArgs = ['-Xdiags:verbose']
}
